// Mist Un-Grammar.

SourceFile =
  Item*

Item =
  Const
| Fn
| Struct
| TypeInvariant
| Macro

Name = 'ident' | 'self'

NameRef = 'ident' | 'self'

Const =
  Attrs
  'const' Name ':' Type
  ('=' initializer:Expr)?
  ';'

Fn =
  Attrs
 'fn' Name ParamList
 ('->' ret:Type)?
 Condition*
 Decreases?
 (body:BlockExpr | ';')

Attr =
  'ghost'
| 'pure'

Attrs = Attr*

ParamList =
 '(' Param* ')'

Param = Attrs Name (':' Type)? ','?

Condition =
  Requires
| Ensures

Requires =
  ('requires' | 'req') CommaExpr*
Ensures =
  ('ensures'  | 'ens') CommaExpr*

CommaExpr =
  Expr ','?

Decreases =
  ('decreases' | 'dec') ('_' | Expr)

Struct =
  Attrs
  'struct' Name GenericArgList? '{' StructField* '}'

StructField = Attrs Name ':' Type ','?

TypeInvariant =
  'invariant' NameRef GenericArgList? BlockExpr

Macro =
  'macro' Name ParamList BlockExpr

BlockExpr =
 '{'
   statements:Stmt*
   tail_expr:Expr?
 '}'

Type =
  NamedType
| Primitive
| Optional
| ListType
| GhostType
| RefType

NamedType =
  Name GenericArgList?

GenericArgList =
  '<' GenericArg* '>'

GenericArg =
  Type ','?

Primitive =
  'int'
| 'bool'

Optional =
  '?' Type

ListType =
  '[' Type ']'

GhostType =
  'ghost' Type

RefType =
  '&' 'mut'? Type

Stmt =
  LetStmt
| Item
| ExprStmt
| AssertStmt
| AssumeStmt
| WhileStmt

LetStmt =
 'let' Name (':' Type)?
 ('=' initializer:Expr)?
 ';'

ExprStmt =
  Expr ';'

AssertStmt =
 'assert' CommaExpr* ';'

AssumeStmt =
 'assume' CommaExpr* ';'

ReturnExpr =
 'return' Expr ';'

WhileStmt =
 'while' Expr
 Invariant*
 Decreases?
 BlockExpr

Invariant =
 'invariant' CommaExpr*

Expr =
  Literal
| IfExpr
| ReturnExpr
| WhileExpr
| PrefixExpr
| BinExpr
| BlockExpr
| RangeExpr
| CallExpr
| ListExpr
| IndexExpr
| NotNullExpr
| FieldExpr
| StructExpr
| ParenExpr
| RefExpr
| IdentExpr
| NullExpr
| ResultExpr
| QuantifierExpr

IfExpr =
  'if' condition:Expr then_branch:BlockExpr
  ('else' else_branch:IfExprElse)?

IfExprElse =
  IfExpr | BlockExpr

WhileExpr =
  'while' condition:Expr body:BlockExpr

PrefixExpr =
  op:('-' | '!') Expr

BinExpr =
  lhs:Expr
  op:(
    '||' | '&&'
  | '==' | '!=' | '<=' | '>=' | '<' | '>'
  | '+' | '*' | '-' | '/' | '%' | '<<' | '>>' | '^' | '|' | '&'
  | '=' | '+=' | '/=' | '*=' | '%=' | '>>=' | '<<=' | '-=' | '|=' | '&=' | '^='
  )
  rhs:Expr

RangeExpr =
  (lhs:Expr)?
  '..'
  (rhs:Expr)?

CallExpr =
  Expr ArgList

ArgList =
  '(' Arg* ')'

Arg =
  Expr ','?

ListExpr =
  '[' CommaExpr* ']'

IndexExpr =
  base:Expr '[' index:Expr ']'

NotNullExpr =
  Expr '!'

FieldExpr =
  Expr '.' field:Name

StructExpr =
  Name '{' fields:StructExprField* '}'

StructExprField =
  Name ':' Expr ','?

ParenExpr =
  '(' Expr ')'

RefExpr =
  '&' 'mut'? Expr

IdentExpr =
  Name

NullExpr = 'null'

ResultExpr = 'result'

QuantifierExpr =
  Quantifier ParamList Expr

Quantifier =
  'forall' | 'exists'

Literal =
  value:(
    'int_number'
  | 'true' | 'false'
  )
